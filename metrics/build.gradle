/*
 * Copyright (c) 2020 CodeHead Systems. All rights reserved
 */

apply plugin: 'idea'

ext {
    appName = "metrics"
}

configurations {
    project.archivesBaseName = "${appName}"
    jar.baseName = "${appName}"
}

dependencies {
    implementation libs.micrometer.core
    implementation libs.metrics.core
    implementation libs.slf4j.api
    implementation libs.dagger // Common files for runtime
    annotationProcessor libs.dagger.compiler

    testImplementation libs.junit.jupiter.api
    testImplementation libs.assertj.core
    testImplementation libs.mockito.junit.jupiter
    testImplementation libs.mockito.core
    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly libs.junit.jupiter.engine
    testImplementation libs.log4j.api
    testImplementation libs.log4j.core
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = appName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Metrics'
                description = 'Classes to integrate micrometer to dropwizard, and make metrics easier to use.'
                url = 'https://github.com/wolpert/metrics'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'wolpert'
                        name = 'Ned Wolpert'
                        email = 'ned.wolpert@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/wolpert/metrics.git'
                    developerConnection = 'scm:git:ssh://github.com/wolpert/metrics.git'
                    url = 'https://github.com/wolpert/metrics/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            name = "ossrh"
            credentials(PasswordCredentials)
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}